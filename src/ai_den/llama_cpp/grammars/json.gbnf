# Based on https://github.com/ggerganov/llama.cpp/blob/master/grammars/json.gbnf
# Modified to allow one optional space at the beginning (useful for sentencepiece tokenizers),
# and no spaces at the end (which can result in the model predicting a sequence of newlines instead of EOS).

root   ::= " "? value
value  ::= object | array | string | number | "true" | "false" | "null"

object ::=
    "{" ws (
        string ws ":" ws value ws
        ("," ws string ws ":" ws value ws)*
    )? "}"

array  ::=
    "[" ws (
        value ws
        ("," ws value ws)*
    )? "]"

string ::=
    "\"" (
        [^"\\] |
        "\\" (["\\/bfnrt] | "u" [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F]) # escapes
    )* "\""

number ::= ("-"? ([0-9] | [1-9] [0-9]*)) ("." [0-9]+)? ([eE] [-+]? [0-9]+)?

ws ::= ([ \t\n] ws)?
